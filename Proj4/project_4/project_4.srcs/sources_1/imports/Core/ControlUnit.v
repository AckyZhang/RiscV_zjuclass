`timescale 1ns / 1ps
//////////////////////////////////////////////////////////////////////////////////
// Module Name: ControlUnit
// Description: RISC-V Instruction Decoder
//////////////////////////////////////////////////////////////////////////////////
`include "Parameters.v"
`define OP_JAL    7'b1101111 //JALçš„æ“ä½œç 
`define OP_JALR   7'b1100111 //JALRçš„æ“ä½œç 
`define OP_Load   7'b0000011 //Loadç±»æŒ‡ä»¤çš„æ“ä½œç ?
`define OP_Store  7'b0100011 //Storeç±»æŒ‡ä»¤çš„æ“ä½œç ?
`define OP_Branch 7'b1100011 //Branchç±»æŒ‡ä»¤çš„æ“ä½œç ?
`define OP_LUI    7'b0110111 //LUIçš„æ“ä½œç 
`define OP_AUIPC  7'b0010111 //AUIPCçš„æ“ä½œç 
`define OP_RegReg 7'b0110011 //å¯„å­˜å™?-å¯„å­˜å™¨ç®—æœ¯æŒ‡ä»¤çš„æ“ä½œç ?
`define OP_RegImm 7'b0010011 //å¯„å­˜å™?-ç«‹å³æ•°ç®—æœ¯æŒ‡ä»¤çš„æ“ä½œç ?
`define OP_vecReg 7'b1010111 //å‘é‡æ“ä½œæŒ‡ä»¤çš„æ“ä½œç 
`define OP_vecImm 7'b1010110 //å‘é‡æ“ä½œæŒ‡ä»¤çš„æ“ä½œç (ç«‹å³æ•°æ“ä½?)
module ControlUnit(
    input wire [6:0] Op,
    input wire [2:0] Fn3,
    input wire [6:0] Fn7,
    output wire JalD,
    output wire JalrD,
    output reg [2:0] RegWriteD,
    output wire MemToRegD,
    output reg [3:0] MemWriteD,
    output wire LoadNpcD,
    output reg [1:0] RegReadD,
    output reg [2:0] BranchTypeD,
    output reg [4:0] AluContrlD,
    output wire [1:0] AluSrc2D,
    output wire AluSrc1D, alu_vec_id,
    output reg [2:0] ImmType
    );

    assign JalD       = (Op == `OP_JAL) ? 1'b1:1'b0;
    assign JalrD      = (Op == `OP_JALR) ? 1'b1:1'b0;
    assign MemToRegD  = (Op == `OP_Load) ? 1'b1:1'b0;
    assign LoadNpcD   = ((Op == `OP_JAL)||(Op == `OP_JALR)) ? 1'b1:1'b0;
    assign AluSrc2D   = (((Op == `OP_RegImm)||(Op == `OP_vecImm))&&(Fn3[1:0] == 2'b01)) ? (2'b01) : (((Op == `OP_RegReg)||(Op == `OP_vecReg)||(Op == `OP_Branch)) ? 2'b00 : 2'b10);//00:ForwardData2 01:Rs2E 10:Imm
    assign AluSrc1D   = (Op == `OP_AUIPC) ? 1'b1:1'b0;//0:ForwardData1 1:PC
    assign alu_vec_id = ((Op == `OP_vecReg)||(Op ==`OP_vecImm)) ? 1'b1:1'b0;

//sequence:R-type,I-type,S-type,B-type,U-type,J-type
//output reg    output sequence:
//                    ImmType
//                    RegWriteD
//                    MemWriteD
//                    RegReadD
//                    BranchTypeD
//                    AluContrlD
    always@(*)
    begin
        case(Op)
            `OP_RegReg,
            `OP_vecReg:    //R-TYPE Reg-Reg/vec
                begin
                    ImmType <= `RTYPE;
                    RegWriteD <= `LW;
                    MemWriteD <= 4'b0000;
                    RegReadD <= 2'b11;
                    BranchTypeD <= `NOBRANCH;
//                  AluContrlD <= CASE BELOW;
                    case(Fn3)
                        3'b000:    //ADD or SUB
                            AluContrlD <= (Fn7 == 7'b0) ? (`ADD):(`SUB);
                        3'b001:    //SLL
                            AluContrlD <= `SLL;
                        3'b010:    //SLT
                            AluContrlD <= `SLT;
                        3'b011:    //SLTU
                            AluContrlD <= `SLTU;
                        3'b100:    //XOR
                            AluContrlD <= `XOR;
                        3'b101:    //SRL or SRA
                            AluContrlD <= (Fn7 == 7'b0) ? (`SRL):(`SRA);
                        3'b110:    //OR
                            AluContrlD <= `OR;
                        3'b111:    //AND
                            AluContrlD <= `AND;
                    endcase
                end
            `OP_RegImm,
            `OP_vecImm:     //I-Type (ALU) Reg-Imm/vecImm
                begin
                    ImmType <= `ITYPE;
                    RegWriteD <= `LW;
                    MemWriteD <= 4'b0000;
                    RegReadD <= 2'b10;
                    BranchTypeD <= `NOBRANCH;
//                  AluContrlD <= CASE BELOW;
                    case(Fn3)
                        3'b000:    //ADDI
                            AluContrlD <= `ADD;
                        3'b001:    //SLLI
                            AluContrlD <= `SLL;
                        3'b010:    //SLTI
                            AluContrlD <= `SLT;
                        3'b011:    //SLTUI
                            AluContrlD <= `SLTU;
                        3'b100:    //XORI
                            AluContrlD <= `XOR;
                        3'b101:    //SRLI or SRAI
                            AluContrlD <= (Fn7 == 7'b0) ? (`SRL):(`SRA);
                        3'b110:    //ORI
                            AluContrlD <= `OR;
                        3'b111:    //ANDI
                            AluContrlD <= `AND;
                    endcase
                end
            `OP_Load:    //I-Type (LOAD) 
                begin
                    ImmType <= `ITYPE;
//                  RegWriteD <= CASE BELOW;
                    MemWriteD <= 4'b0000;
                    RegReadD <= 2'b10;
                    BranchTypeD <= `NOBRANCH;
                    AluContrlD <= `ADD;
                    case(Fn3)
                        3'b000:    //LB
                            RegWriteD <= `LB;
                        3'b001:    //LH
                            RegWriteD <= `LH;
                        3'b010:    //LW
                            RegWriteD <= `LW;
                        3'b100: //LBU
                            RegWriteD <= `LBU;
                        3'b101: //LHU
                            RegWriteD <= `LHU;
                        default:
                            RegWriteD <= `NOREGWRITE;
                    endcase
                end
            `OP_JALR: //I-Type (JALR) 
                begin
                    ImmType <= `ITYPE;
                    RegWriteD <= `LW;
                    MemWriteD <= 4'b0000;
                    RegReadD <= 2'b10;
                    BranchTypeD <= `NOBRANCH;
                    AluContrlD <= `ADD;
                end
            `OP_Store:    //S-Type
                begin
                    ImmType <= `STYPE; 
                    RegWriteD <= `NOREGWRITE;
//                  MemWriteD <= CASE BELOW;
                    RegReadD <= 2'b11;
                    BranchTypeD <= `NOBRANCH;
                    AluContrlD <= `ADD;
                    case(Fn3)
                        3'b000:    //SB
                            MemWriteD <= 4'b0001;
                        3'b001:    //SH
                            MemWriteD <= 4'b0011;
                        3'b010:    //SW
                            MemWriteD <= 4'b1111;
                        default:
                            MemWriteD <= 4'b0000;
                    endcase
                end
            `OP_Branch:    //B-Type
                begin
                    ImmType <= `BTYPE;
                    RegWriteD <= `NOREGWRITE;
                    MemWriteD <= 4'b0000;
                    RegReadD <= 2'b11;
//                  BranchTypeD <= `BEQ;
                    AluContrlD <= `ADD;
                    case(Fn3)
                        3'b000:    //BEQ
                            BranchTypeD <= `BEQ;
                        3'b001:    //BNE
                            BranchTypeD <= `BNE;
                        3'b100:    //BLT
                            BranchTypeD <= `BLT;
                        3'b101:    //BGE
                            BranchTypeD <= `BGE;
                        3'b110:    //BLTU
                            BranchTypeD <= `BLTU;
                        3'b111:    //BGEU
                            BranchTypeD <= `BGEU;
                        default:    //NOBRANCH
                            BranchTypeD <= `NOBRANCH;
                    endcase
                end
            `OP_LUI:    //U-Type (LUI)
                begin
                    ImmType <= `UTYPE;
                    RegWriteD <= `LW;
                    MemWriteD <= 4'b0000;
                    RegReadD <= 2'b00;
                    BranchTypeD <= `NOBRANCH;
                    AluContrlD <= `LUI;
                end
            `OP_AUIPC:    //U-Type (AUIPC)
                begin
                    ImmType <= `UTYPE;
                    RegWriteD <= `LW;
                    MemWriteD <= 4'b0000;
                    RegReadD <= 2'b00;
                    BranchTypeD <= `NOBRANCH;
                    AluContrlD <= `ADD;
                end
            `OP_JAL:    //J-Type (JAL)
                begin
                    ImmType <= `JTYPE;
                    RegWriteD <= `LW;
                    MemWriteD <= 4'b0000;
                    RegReadD <= 2'b00;
                    BranchTypeD <= `NOBRANCH;
                    AluContrlD <= `ADD;
                end
            default:    //NOP
                begin
                    ImmType <= `ITYPE;
                    RegWriteD <= `NOREGWRITE;
                    MemWriteD <= 4'b0000;
                    RegReadD <= 2'b00;
                    BranchTypeD <= `NOBRANCH;
                    AluContrlD <= `ADD;                    
                end
        endcase
    end
endmodule

//åŠŸèƒ½è¯´æ˜
    //ControlUnit       æ˜¯æœ¬CPUçš„æŒ‡ä»¤è¯‘ç å™¨ï¼Œç»„åˆé?»è¾‘ç”µè·¯
//è¾“å…¥
    // Op               æ˜¯æŒ‡ä»¤çš„æ“ä½œç éƒ¨åˆ?
    // Fn3              æ˜¯æŒ‡ä»¤çš„func3éƒ¨åˆ†
    // Fn7              æ˜¯æŒ‡ä»¤çš„func7éƒ¨åˆ†
//è¾“å‡º
    // JalD==1          è¡¨ç¤ºJalæŒ‡ä»¤åˆ°è¾¾IDè¯‘ç é˜¶æ®µ
    // JalrD==1         è¡¨ç¤ºJalræŒ‡ä»¤åˆ°è¾¾IDè¯‘ç é˜¶æ®µ
    // RegWriteD        è¡¨ç¤ºIDé˜¶æ®µçš„æŒ‡ä»¤å¯¹åº”çš„å¯„å­˜å™¨å†™å…¥æ¨¡å¼? ï¼Œæ‰€æœ‰æ¨¡å¼å®šä¹‰åœ¨Parameters.vä¸?
    // MemToRegD==1     è¡¨ç¤ºIDé˜¶æ®µçš„æŒ‡ä»¤éœ€è¦å°†data memoryè¯»å–çš„å?¼å†™å…¥å¯„å­˜å™¨,
    // MemWriteD        å…?4bitï¼Œé‡‡ç”¨ç‹¬çƒ­ç æ ¼å¼ï¼Œå¯¹äºdata memoryçš?32bitå­—æŒ‰byteè¿›è¡Œå†™å…¥,MemWriteD=0001è¡¨ç¤ºåªå†™å…¥æœ€ä½?1ä¸ªbyteï¼Œå’Œxilinx bramçš„æ¥å£ç±»ä¼?
    // LoadNpcD==1      è¡¨ç¤ºå°†NextPCè¾“å‡ºåˆ°ResultM
    // RegReadD[1]==1   è¡¨ç¤ºA1å¯¹åº”çš„å¯„å­˜å™¨å€¼è¢«ä½¿ç”¨åˆ°äº†ï¼ŒRegReadD[0]==1è¡¨ç¤ºA2å¯¹åº”çš„å¯„å­˜å™¨å€¼è¢«ä½¿ç”¨åˆ°äº†ï¼Œç”¨äºforwardçš„å¤„ç?
    // BranchTypeD      è¡¨ç¤ºä¸åŒçš„åˆ†æ”¯ç±»å‹ï¼Œæ‰?æœ‰ç±»å‹å®šä¹‰åœ¨Parameters.vä¸?
    // AluContrlD       è¡¨ç¤ºä¸åŒçš„ALUè®¡ç®—åŠŸèƒ½ï¼Œæ‰€æœ‰ç±»å‹å®šä¹‰åœ¨Parameters.vä¸?
    // AluSrc2D         è¡¨ç¤ºAluè¾“å…¥æº?2çš„é?‰æ‹©
    // AluSrc1D         è¡¨ç¤ºAluè¾“å…¥æº?1çš„é?‰æ‹©
    // ImmType          è¡¨ç¤ºæŒ‡ä»¤çš„ç«‹å³æ•°æ ¼å¼ï¼Œæ‰€æœ‰ç±»å‹å®šä¹‰åœ¨Parameters.vä¸?   
    // alu_vec_id       è¡¨ç¤ºè¯¥æ¡æŒ‡ä»¤æ˜¯å¦ä¸ºå‘é‡æ‰©å±•æŒ‡ä»?